#!/usr/bin/env zsh
# shellcheck shell=sh

# COMPLETIONS
# Init zsh completions
autoload -U compinit
compinit

# Case-insensitive completion
# https://superuser.com/a/1092328
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'

# Menu selection
zstyle ':completion:*' menu yes select
zmodload zsh/complist
# Finish completion and execute with (return)
bindkey -M menuselect '^M' .accept-line
# Cancel selection with (esc)
bindkey -M menuselect '\e' send-break

# INPUT
# Use the string that has already been typed as the prefix for searching
# through commands (i.e. more intelligent Up/Down-arrow behavior)
bindkey "^[[A" history-beginning-search-backward
bindkey "^[OA" history-beginning-search-backward # SSH
bindkey "^[[B" history-beginning-search-forward
bindkey "^[OB" history-beginning-search-forward # SSH

# Reverse through the completions menu using shift-tab
bindkey "^[[Z" reverse-menu-complete

# Move cursor forward/backward by word (⌥→/⌥←)
backward-word-dir () {
	local WORDCHARS=${WORDCHARS//[\/\-]}
	zle backward-word
}
zle -N backward-word-dir
bindkey "^[b" backward-word-dir
forward-word-dir () {
	local WORDCHARS=${WORDCHARS//[\/\-]}
	zle forward-word
}
zle -N forward-word-dir
bindkey "^[f" forward-word-dir

# Move cursor to beginning/end of line (⌘←/⌘→)
bindkey "^[[1~" beginning-of-line
bindkey "^[[4~" end-of-line

# Fix backspace when existing vi mode
bindkey -v "^?" backward-delete-char

# Case-insensitive globbing (used in pathname expansion)
unsetopt CASE_GLOB

# Enable parameter expansion, command substitution and arithmetic expansion in prompt string
setopt PROMPT_SUBST

# AUTOSUGGEST
source "/usr/local/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
ZSH_AUTOSUGGEST_STRATEGY=(match_prev_cmd completion)
ZSH_AUTOSUGGEST_HISTORY_IGNORE="cd *"
ZSH_AUTOSUGGEST_USE_ASYNC=1

# Accept entire suggestion with tab
bindkey "\t" autosuggest-accept

# Accept partial suggestion with ⌘→
ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS+=(forward-word-dir)

# PROMPT
# Inspired by https://github.com/necolas/dotfiles/blob/master/shell/bash_prompt
prompt_git_branch_name() {
	! command git rev-parse --is-inside-work-tree &>/dev/null && return

	local branchName
  branchName="$(command git symbolic-ref --quiet --short HEAD 2> /dev/null || \
		command git rev-parse --short HEAD 2> /dev/null || \
		echo '(unknown)')"

	echo -e "${1}${branchName}"
}

# https://www.anishathalye.com/2015/02/07/an-asynchronous-shell-prompt/
# Print this shell process’ git status
prompt_git_status() {
	if [ -f "/tmp/zsh_prompt_git_status_$$" ]; then
		local s=$(cat "/tmp/zsh_prompt_git_status_$$")
		[ -n "${s}" ] && s=" [${s}]"
		echo -e "${1}${s}"
	fi
}
# Update this shell process’ git status
prompt_git_status_async() {
	! command git rev-parse --is-inside-work-tree &>/dev/null && return

	local s='';

	if [[ "$(command git rev-parse --is-inside-git-dir 2> /dev/null)" == "false" ]]; then
		command git update-index --really-refresh -q &>/dev/null;
		# [+] staged, uncommitted
		! command git diff --quiet --ignore-submodules --cached && s+='+'
		# [!] unstaged
		! command git diff-files --quiet --ignore-submodules -- && s+='!'
		# [?] untracked - this is the most expensive
		[ -n "$(command git ls-files --others --exclude-standard)" ] && s+='?'
		# [$] stashed
		command git rev-parse --verify refs/stash &>/dev/null && s+='$'
	fi
	
	echo "${s}" > "/tmp/zsh_prompt_git_status_$$"
	kill -s USR1 $$
}
# Re-draw the prompt when git status updates are available
TRAPUSR1() {
	PROMPT_GIT_STATUS_ASYNC_PROC=0
	zle && zle reset-prompt
}

set_prompts() {
	if [[ -n "${SSH_CLIENT}" ]]; then
		PROMPT="%4F%n%8F on %2F%m"
	else
		PROMPT="%4F%n"
	fi
  PROMPT+="%8F in ";
  PROMPT+="%2F%(5~|%-1~/.../%3~|%4~)"; # working directory, https://unix.stackexchange.com/a/273567
  PROMPT+='$(prompt_git_branch_name "%8F on %5F")'; # git branch name
	PROMPT+='$(prompt_git_status "%8F")'; # git status
  PROMPT+=$'\n';
  PROMPT+="%f\$ "; # `$` (and reset color)
  export PROMPT;
}
set_prompts
unset set_prompts

PPWD="$PWD" # Remember previous working directory
precmd() {
  if [ -z "${PROMPT_CTR}" ]; then
    PROMPT_CTR=1
  elif [ "${PROMPT_CTR}" -eq 1 ]; then
    echo ""
		# osascript -e 'if app "Terminal" is frontmost then tell app "System Events" to keystroke "u" using command down'
  fi

	# Clear shell process’ git status when working directory changes
	if [[ "${PPWD}" != "${PWD}" ]]; then
		rm -f "/tmp/zsh_prompt_git_status_$$"
	fi
	# Kill unfinished git status update requests
	if [[ "${PROMPT_GIT_STATUS_ASYNC_PROC}" != 0 ]]; then
		kill -s HUP $PROMPT_GIT_STATUS_ASYNC_PROC &>/dev/null || :
	fi
	# Request updated git status in the background
	prompt_git_status_async &!
	PROMPT_GIT_STATUS_ASYNC_PROC=$!
	PPWD="$PWD" # Update previous working direectory
}
